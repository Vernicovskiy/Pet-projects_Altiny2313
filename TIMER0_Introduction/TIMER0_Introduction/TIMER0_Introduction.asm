/*		Мигание светодиодом с использованием таймера T0


Микроконтроллер:	ATtiny2313
Тактовая частота:	1 MHz
*/

.include "tn2313def.inc"

.def	temp	=R16

.cseg
.org	0x00
		rjmp	init	
		reti			;(0х01) Внешнее прерывание 0
		reti			;(0х02) Внешнее прерывание 1
		reti			;(0х03) Захват таймера/счетчика Т1
		reti		    ;(0х04) Совпадение А таймера/счетчика Т1
		reti			;(0х05) Переполнение таймера/счетчика Т1
		rjmp	T0_Ovr	;(0х06) Переполнение таймера/счетчика Т0
		reti			;(0х07) USART, прием завершен
		reti			;(0х08) Регистр данных USART пуст
		reti			;(0х09) USART, передача завершена
		reti			;(0х0A) Прерывание по компаратору
		reti			;(0х0B) Прерывание по изменению на любом контакте
		reti			;(0х0C) Совпадение В таймера/счетчика Т1
		reti			;(0х0D) Совпадение А таймера/счетчика Т0
		reti			;(0х0E) Совпадение В таймера/счетчика Т0
		reti			;(0х0F) USI Стартовая готовность
		reti			;(0х10) USI Переполнение
		reti			;(0х11) EEPROM Готовность
		reti			;(0х12) Переполнение сторожевого таймер

;ИНИЦИАЛИЗАЦИЯ
init:	ldi		temp,RAMEND	;Инициализация стека
		out		SPL,temp
		
		ser		temp		;Порт В на выход
		out		DDRB,temp
		out		PORTB,temp

		ser		temp
		out		PORTD,temp	;Порт D на вход


/*
Регистр TIMSK разрешает/запрещает различные прерывания для таймеров Т0 и Т1 
Описание регистра TIMSK на 227 странице
Если разряды сброшены в ноль, то соответствующие этим разрядам прерывания, запрещены

Для таймера Т0:
TOIE0 	Прерывание по переполнению таймера Т0 
		Переполнение - когда счетный регистр таймера переходит с 255 на 0

OCIE0A	Прерывание по совпадению счетного регистра TCNT0 таймера Т0	с регистром совпадения OCR0A

OCIE0B	Прерывание по совпадению счетного регистра TCNT0 таймера Т0	с регистром совпадения OCR0B
*/

		ldi		temp,(1<<TOIE0)
		out		TIMSK,temp
;В регистре TIMSK установлен бит TOIE0, следовательно,
;при переполнении таймера Т0 сработает прерывание 
;по переполнению таймера Т0 (вектор прерывания расположен по адресу 0х06)



/*
Регистр TCCR0B - регистр управления таймером Т0
Описание регистра TCCR0B на 238 странице
Биты CS02,CS01,CS00 отвечают за предделитель частоты таймера Т0

CS02=0 CS01=0 CS00=0	ТАЙМЕР ОСТАНОВЛЕН
CS02=0 CS01=0 CS00=1	ДЕЛИТЕЛЬ  1
CS02=0 CS01=1 CS00=0	ДЕЛИТЕЛЬ  8
CS02=0 CS01=1 CS00=1	ДЕЛИТЕЛЬ  64
CS02=1 CS01=0 CS00=0	ДЕЛИТЕЛЬ  256
CS02=1 CS01=0 CS00=1	ДЕЛИТЕЛЬ  1024

CS02=1 CS01=1 CS00=0	ВНЕШНИЙ ТАКТОВЫЙ СИГНАЛ НА ВХОДЕ Т0. СЧЕТ ПО СПАДАЮЩЕМУ ФРОНТУ
CS02=1 CS01=1 CS00=1	ВНЕШНИЙ ТАКТОВЫЙ СИГНАЛ НА ВХОДЕ Т0. СЧЕТ ПО НАРАСТАЮЩЕМУ ФРОНТУ
*/

		ldi		temp,(0<<CS02|0<<CS01|1<<CS00) ;деление 1
		out		TCCR0B,temp
;CS02=0 CS01=0 CS00=1 , следовательно, таймер работает на частоте: (тактовая частота)/1 = тактовая частота
;ТАЙМЕР НАЧНЕТ СЧИТАТЬ ТОЛЬКО ПОСЛЕ ТОГО, КАК В РЕГИСТР TCCR0B
;БУДЕТ ЗАПИСАН КОЭФФИЦИЕНТ ДЕЛЕНИЯ ОТЛИЧНЫЙ ОТ НУЛЯ
		
		nop
		nop
		nop
		nop
		nop
		nop
		nop

		clr		temp
		out		TCNT0,temp	;Принудительно обнулили таймер
		nop
		nop
		nop
		nop
		nop

		ldi		temp,100
		out		TCNT0,temp	;Принудительно задали начальное значение
		nop
		nop
		nop
		nop
		nop
		
		clr		temp
		out		TCCR0B,temp	;Таймер остановлен
		nop
		nop
		nop
		nop
		nop

		ldi		temp,(0<<CS02|0<<CS01|1<<CS00)
		out		TCCR0B,temp
;CS02=0 CS01=0 CS00=1 ДЕЛИТЕЛЬ  1 => таймер работает на частоте (тактовая частота)/1 = 1000000/1 = 1000000 Гц
;CS02=0 CS01=1 CS00=0 ДЕЛИТЕЛЬ  8 => таймер работает на частоте (тактовая частота)/8 = 1000000/8 = 125000 Гц
;CS02=1 CS01=0 CS00=1 ДЕЛИТЕЛЬ  1024 => таймер работает на частоте (тактовая частота)/1024 = 1000000/1024 = 967 Гц
;CS02=1 CS01=1 CS00=0 ВНЕШНИЙ ТАКТОВЫЙ СИГНАЛ НА ВХОДЕ Т0. СЧЕТ ПО СПАДАЮЩЕМУ ФРОНТУ


		sei		;ГЛОБАЛЬНОЕ РАЗРЕШЕНИЕ ПРЕРЫВАНИЙ
;Если забыть установить командой sei бит I, то прерывания происходить не будут
		
	;	ldi		temp,250
	;	out		TCNT0,temp	;Принудительно задали начальное значение

MAIN:	rjmp	MAIN


;Переполнение таймера происходит один раз за 256 счетов таймера, следовательно, 
;частота вызова прерывания = (частота таймера)/256

T0_Ovr:
	;	ldi		temp,250
	;	out		TCNT0,temp	;Принудительно задали начальное значение отличное от нуля

		sbi		PINB,0		;Изменяем состояние вывода на противоположное
		reti				;Завершаем прерывание

;Так как для формирования на выводе одного полного периода сигнала требуется два 
;переполнения таймера, то частота на выводе в 2 раза ниже частоты переполнения таймера
;Таким образом, частота на выводе равна: 
;F = (тактовая частота)/[(коэф.деления таймера) * 256 * 2]
;Для тактовой частоты 1 MHz и коэф. деления 8, частота на выводе составит:
;F = 1000000/(8 * 256 * 2) = 244,140625 Hz
