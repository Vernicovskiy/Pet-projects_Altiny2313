/*	Работа таймера Т0 в режиме Широтно-Импульсной Модуляции (ШИМ)
				Pulse Width Modulation (PWM)

Тип ШИМ: Режим корректировки по фазе (Phase Correct PWM)

Микроконтроллер:	ATtiny2313
Тактовая частота:	1 MHz
*/

.include "tn2313def.inc"

.def	temp	=R16
.def	count	=R17			;Счетчик для хранения значения 
.def	loop	=R18

.equ	BT_UP	=0				;Увеличение значения регистров совпадения
.equ	BT_DW	=1				;Уменьшение значения регистров совпадения


		ldi		temp,RAMEND		;Инициализация стека
		out		SPL,temp
		
		ser		temp			;Порт В на выход
		out		DDRB,temp
		ser		temp			;На всех выводах порта В - высокий уровень 
		out		PORTB,temp

		ldi		temp,(1<<PD5)	;Все выводы порта D, за исключением PD5, на вход.
		out		DDRD,temp		;PD5 на выход
		ser		temp			;На всех выводах порта D включены подтягивающие резисторы.
		out		PORTD,temp


		ldi		temp,(1<<COM0B1|1<<COM0B0|1<<COM0A1|0<<COM0A0|1<<WGM00)
		out		TCCR0A,temp
/*В регистре TCCR0А установлен бит WGM00, следовательно,
таймер работает в режиме широтно-импульсной модуляции.
В этом режиме таймер вначале увеличивает свое значение от 0 до 0xFF, 
затем начинает считать в обратную сторону от 0xFF до 0.

Когда значение счетного регистра совпадает с величинами в регистрах
OCR0A или OCR0B, на выводах OC0A или OC0B (в зависимости от состояния битов 
СОМ0В0,СОМ0В1 и СОМ0A0,СОМ0A1) cформируется сигнал ШИМ.

Так как бит COM0A0 = 0 и COM0A1 = 1, то при прямом счете (от 0 до 0xFF),
при совпадении счетного регистра с OCR0A, вывод OC0A (PB2) будет сброшен в ноль
При обратном счете (от 0xFF до 0), при совпадении счетного регистра 
с OCR0A, вывод OC0A (PB2) будет установлен в 1.

Бит COM0B1 = 1 и COM0B0 = 1, следовательно, при прямом счете (от 0 до 0xFF),
при совпадении счетного регистра с OCR0В, вывод OC0В (PD5) будет установлен в 1.
При обратном счете (от 0xFF до 0), при совпадении счетного регистра 
с OCR0В, вывод OC0В (PD5) будет сброшен в ноль.*/


		ldi		temp,(0<<CS02|0<<CS01|1<<CS00) ;деление 1
		out		TCCR0B,temp
/*Для режима Phase Correct  PWM несущая частота определяется f = F/510,
где F - частота работы таймера
Для частоты таймера 1 MHz частота ШИМ: f = 1000000/510 = 1960 Hz*/

		ldi		count,0			;В регистр счетчика загружаем начальное значение





;#### ОСНОВНОЙ ЦИКЛ ####
MAIN:	rcall	klav
		out		OCR0A,count		;Переписываем значение count в OCR0A и OCR0B
		out		OCR0B,count
		rjmp	MAIN





;Подпрограмма опроса клавиатуры
klav:	sbis	PIND,BT_UP
		rjmp	UP
		sbis	PIND,BT_DW
		rjmp	DW
		ret

;Уменьшение скважности (увличение эквивалентного напряжения)
UP:		rcall	delay			;Задержка против дребезга
		subi	count,-8		;Увеличение count на 8
		sbis	PIND,BT_UP		;Ожидание отпускания кнопки
		rjmp	PC-1
		rcall	delay			;Задержка против дребезга
		ret

;Увеличение скважности (уменьшение эквивалентного напряжения)
DW:		rcall	delay			;Задержка против дребезга
		subi	count,8			;Уменьшение count на 8
		sbis	PIND,BT_DW		;Ожидание отпускания кнопки
		rjmp	PC-1
		rcall	delay			;Задержка против дребезга
		ret

delay:	ser		loop
		ser		temp
		dec		temp
		brne	PC-1			;Переход на строку выше
		dec		loop
		brne	delay-1			;Переход на строку ниже после метки delay
		ret


