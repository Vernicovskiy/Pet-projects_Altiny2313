/*
ИСПОЛЬЗОВАНИЕ АНАЛОГОВО КОМПАРАТОРА

Каждый раз, когда напряжение на инверсном входе (PB1) будет уменьшатся и 
пересекать уровень напряжения на прямом входе (1,11 В), светодиод будет
переключать свое состояние на противоположное.

Неинвертирующий (прямой) вход аналогового компаратора AIN0 (PB0)
Инвертирующий вход аналогового компаратора AIN1	(PB1)

Микроконтроллер:	ATtiny2313
Тактовая частота:	1 MHz
*/

.include "tn2313def.inc"

.def	temp	=R16

.equ	VD		=PD5

.cseg
.org	0x00
		rjmp	init	
		reti				;(0х01) Внешнее прерывание 0 
		reti				;(0х02) Внешнее прерывание 1
		reti				;(0х03) Захват таймера/счетчика Т1
		reti			    ;(0х04) Совпадение А таймера/счетчика Т1
		reti				;(0х05) Переполнение таймера/счетчика Т1
		reti				;(0х06) Переполнение таймера/счетчика Т0
		reti				;(0х07) USART, прием завершен
		reti				;(0х08) Регистр данных USART пуст
		reti				;(0х09) USART, передача завершена
		rjmp	Comparator	;(0х0A) Прерывание по компаратору
		reti				;(0х0B) Прерывание по изменению на любом контакте
		reti				;(0х0C) Совпадение В таймера/счетчика Т1
		reti				;(0х0D) Совпадение А таймера/счетчика Т0
		reti				;(0х0E) Совпадение В таймера/счетчика Т0
		reti				;(0х0F) USI Стартовая готовность
		reti				;(0х10) USI Переполнение
		reti				;(0х11) EEPROM Готовность
		reti				;(0х12) Переполнение сторожевого таймер


;ИНИЦИАЛИЗАЦИЯ
init:	ldi		temp,RAMEND	;Инициализация стека
		out		SPL,temp
		
		ser		temp		;Порт D на выход
		out		DDRD,temp
		clr		temp		;На всех выводах - низкий уровень 
		out		PORTD,temp

		clr		temp		;Порт В на вход
		out		DDRB,temp
		ldi		temp,~(1<<PB0|1<<PB1)
		out		PORTB,temp	;На разрядах PB0 и PB1 подтягивающие резисторы 
							;отключены. На остальных выводах - включены 

;Если вывод PB1 как вход компаратора не используется, его желательно
;не оставлять в третьем состоянии и "висящем в воздухе".


/*Для работы компаратора, необходимо разряды порта В  PB0 и PB1 (разряды, к 
которым подключены выводы аналогового компаратора) сконфигурировать на вход
и отключить подтягивающие резисторы.
Но если используется только один инвертирующий вход (AIN1, PB1) 
(когда вход AIN0 (PB0) подключен к внутреннему ИОН),
то вывод PB0 можно использовать как обычный вывод порта В*/
		

		ldi		temp,(1<<ACBG|1<<ACIE|1<<ACIS1|1<<ACIS0)
		out		ACSR,temp
/*Включение компаратора осуществляется установкой бита ACD в НОЛЬ.
После включения питания компаратор ВСЕГДА включен

Установка бита ACBG в 1 подключает к неинвертирующему входу (PB0, AIN0) 
компаратора внутренний Источник Опорного Напряжения (ИОН) равный 1,11В
При этом неинвертирующий вход компаратора отключается от компаратора и 
работает как обычный вывод PB0.

Бит ACIE, установленный в 1 разрешает прерывание от компаратора

Биты ACIS1 = 1, ACIS0 = 1 устанавливают прерывание по изменению состояния
выхода компаратора с 0 на 1
*/


		sei		;ГЛОБАЛЬНОЕ РАЗРЕШЕНИЕ ПРЕРЫВАНИЙ
		

;ОСНОВНОЙ ЦИКЛ
MAIN:	rjmp	MAIN


/*Прерывание срабатывает при изменении состояния выхода 
компаратора с 0 на 1.
(в тот момент, когда напряжение на инвертирующем входе
станет меньше напряжения на прямом входе (1,11 В))*/
Comparator:
		sbi		PIND,VD		;Изменяем состояние вывода на противоположное
		reti
