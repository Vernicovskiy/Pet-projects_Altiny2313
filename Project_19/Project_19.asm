/*
Описание:
Краткое ознакомление с 16-и разрядным таймером Т1

Таймер Т1 работает в режиме NORMAL 

Включено прерывание по переполнению, прерывание по захвату,
прерывание по совпадению в канале А и в канале В

ОЧЕНЬ ВАЖНОЕ ПРАВИЛО:
ПРИ ЗАПИСИ В 16-РАЗРЯДНЫЙ РЕГИСТР ПЕРВЫМ ДОЛЖЕН ЗАПИСЫВАТЬСЯ СТАРШИЙ БАЙТ
ПРИ ЧТЕНИИ ИЗ 16-РАЗРЯДНОГО РЕГИСТРА ПЕРВЫМ ДОЛЖЕН СЧИТЫВАТЬСЯ МЛАДШИЙ БАЙТ

Это правило обязательно нужно соблюдать, когда производиться чтение или запись 
16-разрядного регистра, который в момент записи или считывания может измениться.

Микроконтроллер:	ATtiny2313
Тактовая частота:	1 MHz
*/

.include "tn2313def.inc"

.def	temp		=R16
.def	capt_L		=R17	;Младший регистр для хранения младшего байта регистра захвата
.def	capt_H		=R18	;Старший регистр для хранения старшего байта регистра захвата

.equ	VD			=1		;Разряд порта В со светодиодом

.equ	Const_A		=1000	;Константа совпадения в канале А

.cseg
.org	0x00
		rjmp	init	
		reti				;(0х01) Внешнее прерывание 0
		reti				;(0х02) Внешнее прерывание 1
		rjmp	T1_capt		;(0х03) Захват таймера/счетчика Т1
		rjmp	T1_match_A  ;(0х04) Совпадение А таймера/счетчика Т1
		rjmp	T1_over		;(0х05) Переполнение таймера/счетчика Т1
		reti				;(0х06) Переполнение таймера/счетчика Т0
		reti				;(0х07) USART, прием завершен
		reti				;(0х08) Регистр данных USART пуст
		reti				;(0х09) USART, передача завершена
		reti				;(0х0A) Прерывание по компаратору
		reti				;(0х0B) Прерывание по изменению на любом контакте
		rjmp	T1_match_B	;(0х0C) Совпадение В таймера/счетчика Т1
		reti				;(0х0D) Совпадение А таймера/счетчика Т0
		reti				;(0х0E) Совпадение В таймера/счетчика Т0
		reti				;(0х0F) USI Стартовая готовность
		reti				;(0х10) USI Переполнение
		reti				;(0х11) EEPROM Готовность
		reti				;(0х12) Переполнение сторожевого таймер


;ИНИЦИАЛИЗАЦИЯ
init:	ldi		temp,RAMEND	;Инициализация стека
		out		SPL,temp
		
		ser		temp			;Порт В на выход
		out		DDRB,temp
		ldi		temp,~(1<<PD6)	;Весь порт D на выход, кроме PD6 (вход захвата)
		out		DDRD,temp
		clr		temp
		out		PORTB,temp
		ldi		temp,(1<<PD6)	;На выводе PD6 - подтягивающий резистор
		out		PORTD,temp

/*################################################################################
Регистр TIMSK включает/выключает различные прерывания для таймера Т0 и Т1 [227]
Если биты сброшены в 0, то соответствующие этим битам прерывания запрещены

Для таймера Т1:
TOIE1 	Прерывание по переполнению таймера Т0 

OCIE1A	Прерывание по совпадению счетного регистра TCNT1 таймера Т1
		с регистром совпадения OCR1A

OCIE1B	Прерывание по совпадению счетного регистра TCNT1 таймера Т1
		с регистром совпадения OCR1B

ICIE1	Прерывание по захвату   таймера Т1
################################################################################*/


		ldi		temp,(1<<ICIE1|1<<OCIE1A|1<<OCIE1B|1<<TOIE1)
		out		TIMSK,temp
/*Бит OCIE1A разрешает прерывание по совпадению в канале А (0х04) таймера Т1

Бит OCIE1B разрешает прерывание по совпадению в канале B (0х0С) таймера Т1

Бит TOIE1 разрешает прерывание по переполнению таймера Т1 (0х05)

Бит ICIE1 разрешает прерывание по захвату (0х03) таймера Т1
*/



		ldi		temp,(0<<COM1B1|1<<COM1B0)
		out		TCCR1A,temp
/*В регистре TCCR1А установлен бит COM1B0, следовательно, 
в момент совпадения счетного регистра ТСNT1 с регистром совпадения 
OCR1B, произойдет переключение вывода PB4 (ОС1В, 16-й вывод) порта B.
Что бы вывод переключался этот вывод должен быть сконфигурирован на 
выход*/

		
		ldi		temp,high(Const_A)
		out		OCR1AH,temp
		ldi		temp,low(Const_A)
		out		OCR1AL,temp
/*В регистр совпадения OCR1A записана константа Const_A.
При совпадении регистра сравнения OCR1A со счетным регистром ТСNT1 
произойдет прерывание по совпадению в канале A*/


		ldi		temp,high(30000)
		out		OCR1BH,temp
		ldi		temp,low(30000)
		out		OCR1BL,temp
/*В регистр совпадения OCR1B записано число 30000.
При совпадении регистра сравнения OCR1B со счетным регистром ТСNT1 
произойдет прерывание по совпадению в канале В*/


;После всех настроек запускаем таймер в работу:

		ldi		temp,(1<<ICES1|0<<CS12|0<<CS11|1<<CS10) ;деление 1
		out		TCCR1B,temp
/*В регистре TCCR0B, устанавливаем желаемый коэффициент деления.

Так же становлен бит ICES1 определяющий в качестве активного 
фронта захвата сигнала перепад с 0 на 1.
Таким образом, захват (сохранение состояния счетного регистра ТСNT1
в регистре захвата ICR1) осуществляется изменением сигнала с 0 на 1 
на выводе захвата PD6 (ICP)*/

		
		sei		;ГЛОБАЛЬНОЕ РАЗРЕШЕНИЕ ПРЕРЫВАНИЙ
		

;### ОСНОВНОЙ ЦИКЛ ###
MAIN:	rjmp	MAIN


;### ПРЕРЫВАНИЕ ПО СОВПАДЕНИЮ В КАНАЛЕ A ###

T1_match_A:
		sbi		PINB,VD	;Изменяем состояние вывода на противоположное
		reti			;Завершаем прерывание


;### ПРЕРЫВАНИЕ ПО СОВПАДЕНИЮ В КАНАЛЕ В ###

T1_match_B:
		reti
		

;### ПРЕРЫВАНИЕ ПО ЗАХВАТУ ####
/*В момент изменения состояния на выводе PD6 (ICP)
производится сохранение состояния счетного регистра ТСNT1
в регистре захвата ICR1

Все действия перезаписи производятся на работающем таймере, поэтому еще раз
напоминаю правило:
ПРИ ЗАПИСИ В 16-РАЗРЯДНЫЙ РЕГИСТР ПЕРВЫМ ДОЛЖЕН ЗАПИСЫВАТЬСЯ СТАРШИЙ БАЙТ
ПРИ ЧТЕНИИ ИЗ 16-РАЗРЯДНОГО РЕГИСТРА ПЕРВЫМ ДОЛЖЕН СЧИТЫВАТЬСЯ МЛАДШИЙ БАЙТ*/

T1_capt:clr		temp			;Таймер можно обнулять и простой перезаписью 
		out		TCNT1H,temp		;Записываем сначала СТАРШИЙ регистр
		out		TCNT1L,temp		;Затем записываем МЛАДШИЙ регистр

		in		capt_L,ICR1L	;Сохраняем сначала содержимое МЛАДШЕГО регистра захвата
		in		capt_H,ICR1H	;Сохраняем сначала содержимое СТАРШЕГО регистра захвата
		reti


;#### ПРЕРЫВАНИЕ ПО ПЕРЕПОЛНЕНИЮ ####
/*Частота вызова прерывания по переполнению = частота работы таймера/65536 = 
=1000 000 / 65536 = 15,258 Гц
Так же можно регулировать частоту срабатывания прерывания по переполнению
простой перезаписью таймера.*/

T1_over:
	;	ldi		temp,0x30		
	;	out		TCNT1H,temp
	;	ldi		temp,0x75
	;	out		TCNT1L,temp
		reti
