/* Считывание ID-кода микросхемы и вывод на индикатор типа HD44780

Микроконтроллер		ATtiny2313
Тактовая частота	8 MHz
Индикатор			типа HD44780 (2 строки, 16 символов)
*/
.include "tn2313def.inc"


.def	temp	=R16
.def	loop	=R17


.dseg		.org	0x60
mem:		.byte	8				;8 байт в памяти данных для хранения прочитанного ID-кода

.cseg		rjmp	start

.include "iButton.inc"				;Подключение библиотечного файла для работы с iButton
.include "HD44780.inc"				;Подключение библиотечного файла для работы с индикатором

start:		ldi		temp,RAMEND
			out		SPL,temp

			Init_HD44780			;Инициализация индикатора

;Основной цикл
main:		Read_ID		mem			;Считывание 8 байт ID-кода
			brtc	PC+2			;Проверка на ошибку считывания
			rcall	Clear_ID		;Очистка буфера хранения ID-кода
			rcall	Print_ID		;Вывод на индикатор 8 байт ID-кода

			Second_Line				;Переход на вторую строку индикатора
			Check_ID	mem,ID0		;Сравнение принятого ID-кода с кодом в памяти
			brts	Print_OK		;Если код совпадает, то переход на Print_OK
			Row		Fail			;Вывод сообщения при несовпадении кодов
			rjmp	main
Print_OK:	Row		OK				;Вывод строки сообщения OK
			rjmp	main

;Печать ID-кода
Print_ID:	First_Line				;Переход на первую строку индикатора
			ldi		ZH,High(mem)	;Загрузка в регистр Z адреса первого байта ID-кода в памяти данных 
			ldi		ZL,Low (mem)
			ldi		R20,8			;В регистр loop загружаем начальное значение
zzz:		ld		temp,Z+			;Считывание в регистр temp байта из памяти данных
			rcall	prHEX			;Вывод байта на печать
			dec		R20
			brne	zzz
			ret

;Обнуление 8 байт памяти данных 
Clear_ID:	ldi		ZH,High(mem)	;Загрузка в регистр Z адреса первого байта памяти данных 
			ldi		ZL,Low (mem)
			ldi		loop,8			;В регистр loop загружаем начальное значение
			ldi		R16,0
			st		Z+,R16			;Обнуление ячейки памяти данных
			dec		loop
			brne	PC-2
			ret


Fail:	.db		"Unknown ID-code ",0,0					;Сообщение ошибки
OK:		.db		"      OK !      ",0,0					;Сообщение при совпадении кода

ID0:	.db		0x01,0xF5,0x77,0x0F,0x0F,0x00,0x00,0x5D	;8-байтный ID-код микросхемы
														;Первым записывается младший байт (групповой код)
