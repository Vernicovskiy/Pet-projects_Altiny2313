/*	БИБЛИОТЕКА ПРОГРАММНОЙ ЭМУЛЯЦИИ ПРОТОКОЛА 1-WIRE

БИБЛИОТЕКА НЕ РАБОТАЕТ НА МК с ТАКТОВОЙ ЧАСТОТОЙ НИЖЕ 2 MHz

В ПОДПРОГРАММАХ ИСПОЛЬЗУЮТСЯ РЕГИСТРЫ R16, R17, R18

Перед использованием библиотеки произвести следующие настройки:
1. Определить вывод порта, который выполняет функцию линии данных
2. Определить константу задержки Const_Delay, в зависимости от тактовой частоты
*/
;#### ОПРЕДЕЛЕНИЕ ВЫВОДА ЛИНИИ ДАННЫХ ####
.equ	PIN_Micro_Lan	=PIND
.equ	PORT_Micro_Lan	=PORTD
.equ	DDR_Micro_Lan	=DDRD
.equ    IO_Micro_Lan	=6			;Номер вывода порта

;#### ОПРЕДЕЛЕНИЕ КОНСТАНТЫ ВРЕМЕННОЙ ЗАДЕРЖКИ ####
;Тактовая частота 2  MHz	=2
;Тактовая частота 4  MHz	=4
;Тактовая частота 8  MHz	=8
;Тактовая частота 16 MHz	=16
.equ	Const_Delay			=8


;============= ОПРЕДЕЛЕНИЯ ВРЕМЕННЫХ ЗАДЕРЖЕК =================
.macro		Delay_Lan_3us			;ЗАДЕРЖКА 3 мкс
			ldi		R18,Const_Delay
			dec		R18
			brne	PC-1
.endm

delay_700_us:	ldi		R18,180		;ЗАДЕРЖКА 700 мкс
				rjmp	PC+2
delay_75_us:	ldi		R18,19		;ЗАДЕРЖКА 75 мкс
				push	R18
				Delay_Lan_3us
				pop		R18
				dec		R18
				brne	delay_75_us+1
				ret
				
;=================== МАКРООПРЕДЕЛЕНИЯ ==========================
.macro	Wire_UP						;ВЫСОКИЙ УРОВЕНЬ, ВКЛЮЧЕНИЕ ПОДТЯГИВАЮЩЕГО РЕЗИСТОРА
		cbi		DDR_Micro_Lan,IO_Micro_Lan		;Перевод линии на вход
		sbi		PORT_Micro_Lan,IO_Micro_Lan		;Подключение внутреннего подтягивающего резистора
.endm

.macro	Wire_DW						;НИЗКИЙ УРОВЕНЬ, ПОДСАДКА ЛИНИИ
		cbi		PORT_Micro_Lan,IO_Micro_Lan		;Установка низкого уровня
		sbi		DDR_Micro_Lan,IO_Micro_Lan		;Перевод линии на выход
.endm

.macro	Wire_UP_Power				;ВЫСОКИЙ СИЛОВОЙ УРОВЕНЬ (АКТИВНАЯ ПОДТЯЖКА)
		sbi	PORT_Micro_Lan,IO_Micro_Lan			;Установка высокого уровня
		sbi	DDR_Micro_Lan,IO_Micro_Lan			;Перевод линии на выход
.endm

.macro	Check_Lan					;СЧИТЫВАНИЕ УРОВНЯ С ЛИНИИ
		clc
		sbic	PIN_Micro_Lan,IO_Micro_Lan		;Пропуск следующей команды, если на линии 0
		sec
.endm	

;===================== ОСНОВНОЙ КОД ============================
;#### НАЧАЛЬНЫЙ СБРОС ####
ResLAN:		Wire_DW					;Подсадка линии
			rcall	delay_700_us	;Задержка 700 мкс
			cli						;Запрет прерываний
			Wire_UP					;Отпуск линии
			rcall	delay_75_us 	;Задержка 75 мкс
			Check_Lan 				;Проверка линии
			sei						;Разрешение прерываний
			ldi		R16,1			;Если на линии 1, то выход с ошибкой "Нет датчика". Код ошибки 1
			brcs	Wire_Err		;Нет датчика
			rcall	delay_700_us 	;Задержка 700 мкс
			ldi		R16,2			;Короткое замыкание. Код ошибки 2
			Check_Lan 				;Проверка линии
			brcc	Wire_Err		;Короткое замыкание
Wire_OK:	ldi		R16,0			;Код нормального завершения сброса. Код 0
Wire_Err:	ret

;#### ЗАПИСЬ БАЙТА ####
wr8LAN:		ldi		R17,8		;Счетчик переданных бит
Nxt_WR_bit:	lsr		R16			;Сдвиг вправо. Передача младшим битом вперед
			brcs	Bit_is_1
Bit_is_0:	rcall	Slot_0		;Запись слота 0
			rjmp	PC+2
Bit_is_1:	rcall	Slot_1		;Запись слота 1
			dec		R17
			brne	Nxt_WR_bit
			ret

;#### ЗАПИСЬ ОТДЕЛЬНОГО БИТА ####
wr1LAN:		brcs	PC+2
			rjmp	Slot_0		;Запись слота 0
			rjmp	Slot_1		;Запись слота 1

;#### ЧТЕНИЕ БАЙТА ####
rd8LAN:		ldi		R17,8		;Счетчик принятых бит
			rcall	Rd_Slot		;Чтение слота
			ror		R16			;Сдвиг принятого бита вправо
			dec		R17
			brne	PC-3
			ret

;#### ЧТЕНИЕ ОТДЕЛЬНОГО БИТА ####
rd1LAN:		rjmp  Rd_Slot

;#### ЧТЕНИЕ СЛОТА ####
Rd_Slot:	cli						;Запрет прерываний на время чтения слота
			Wire_DW					;Подсадка линии				(00 us)
			Delay_Lan_3us			;Задержка 3 мкс				(03 us)
			Wire_UP					;Отпускание линии
			Delay_Lan_3us			;Задержка 3 мкс				(06 us)
			Delay_Lan_3us			;Задержка 3 мкс				(09 us)
			Delay_Lan_3us			;Задержка 3 мкс				(12 us)
			Check_Lan				;Чтение состояния линии	на 12 мкс от начала слота
			sei						;Разрешение прерываний
			rcall	delay_75_us 	;Задержка 75 мкс для формирования слота
			ret

;#### ЗАПИСЬ 0 В СЛОТ ####
Slot_0:		cli						;Запрещение прерываний на время записи слота 0
			Wire_DW					;Подсадка линии
			rcall	delay_75_us 	;Задержка 75 мкс
			Wire_UP_Power			;Активная подтяжка вверх для обеспечения большей крутизны фронта (+ небольшая временная задержка для высокой тактовой частоты)
			Wire_UP					;Отпускание линии
			sei						;Разрешение прерываний
			ret

;#### ЗАПИСЬ 1 В СЛОТ ####
Slot_1:		cli						;Запрещение прерываний на время записи слота 1
			Wire_DW					;Подсадка линии
			Delay_Lan_3us			;Задержка 3 мкс
			Wire_UP_Power			;Активная подтяжка вверх
			Wire_UP					;Отпускание линии
			sei						;Разрешение прерываний
			rcall	delay_75_us 	;Задержка 75 мкс
			ret
