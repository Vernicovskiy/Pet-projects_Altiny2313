/*			РЕЖИМ ПРЕРЫВАНИЕ ПО СОВПАДЕНИЮ

При прерывании по совпадению с регистром совпадения OCR0B
происходит переход программы по вектору T0_capt_B. Так же происходит 
изменение вывода PD5 (ОС0В, 9-й вывод) порта D.

При прерывании по совпадению с регистром совпадения OCR0А
происходит переход программы по вектору T0_capt_A Так же происходит 
обнуление счетного регистра TCNT0. И изменение вывода 
PB2 (ОС0A, 14-й вывод) порта B.

Микроконтроллер:	ATtiny2313
Тактовая частота:	1 MHz
*/

.include "tn2313def.inc"

.def	temp	=R16

.cseg
.org	0x00
		rjmp	init	
		reti				;(0х01) Внешнее прерывание 0
		reti				;(0х02) Внешнее прерывание 1
		reti				;(0х03) Захват таймера/счетчика Т1
		reti			    ;(0х04) Совпадение А таймера/счетчика Т1
		reti				;(0х05) Переполнение таймера/счетчика Т1
		rjmp	T0_Ovr		;(0х06) Переполнение таймера/счетчика Т0
		reti				;(0х07) USART, прием завершен
		reti				;(0х08) Регистр данных USART пуст
		reti				;(0х09) USART, передача завершена
		reti				;(0х0A) Прерывание по компаратору
		reti				;(0х0B) Прерывание по изменению на любом контакте
		reti				;(0х0C) Совпадение В таймера/счетчика Т1
		rjmp	T0_capt_A	;(0х0D) Совпадение А таймера/счетчика Т0
		rjmp	T0_capt_B	;(0х0E) Совпадение В таймера/счетчика Т0
		reti				;(0х0F) USI Стартовая готовность
		reti				;(0х10) USI Переполнение
		reti				;(0х11) EEPROM Готовность
		reti				;(0х12) Переполнение сторожевого таймер


;ИНИЦИАЛИЗАЦИЯ
init:	ldi		temp,RAMEND	;Инициализация стека
		out		SPL,temp
		
		ser		temp		;Порт В и D на выход
		out		DDRB,temp
		out		DDRD,temp
		ser		temp
		out		PORTB,temp
		out		PORTD,temp

/*################################################################################
Регистр TIMSK включает/выключает различные прерывания для таймера Т0 и Т1 [227]
Если биты сброшены в 0, то соответствующие этим битам прерывания запрещены

Для таймера Т0:
TOIE0 	Прерывание по переполнению таймера Т0 

OCIE0A	Прерывание по совпадению счетного регистра TCNT0 таймера Т0
		с регистром совпадения OCR0A

OCIE0B	Прерывание по совпадению счетного регистра TCNT0 таймера Т0
		с регистром совпадения OCR0B
################################################################################*/

		ldi		temp,(1<<OCIE0A|1<<OCIE0B|1<<TOIE0)
		out		TIMSK,temp
/*OCIE0A=1  => при совпадении значения регистра ТСNT0 с OCR0A сработает прерывание 
по совпадению в канале А (0х0D) таймера Т0

OCIE0B=1  => при совпадении значения регистра ТСNT0 с OCR0B сработает прерывание 
по совпадению в канале B (0х0E)

TOIE0=1  => при переполнении TCNT0 сработает прерывание по переполнению
*/



		ldi		temp,(0<<COM0B1|1<<COM0B0|0<<COM0A1|1<<COM0A0|1<<WGM01)
		out		TCCR0A,temp
/*В регистре TCCR0А установлен бит WGM01, следовательно,
таймер досчитает только до значения, записанного в регистре 
TCCR0A, а дальше произойдет сброс таймера.

СОМ0В0 = 1, следовательно, в момент совпадения счетного 
регистра ТСNT0 с регистром совпадения OCR0B, произойдет 
переключение вывода PD5 (ОС0В, 9-й вывод).
Что бы вывод переключался этот вывод должен быть сконфигурирован на 
выход

СОМ0A0 = 1, следовательно, в момент совпадения счетного 
регистра ТСNT0 с регистром совпадения OCR0A, произойдет 
переключение вывода PB2 (ОС0A, 14-й вывод).
Что бы вывод переключался этот вывод должен быть сконфигурирован на 
выход*/


		ldi		temp,200
		out		OCR0A,temp
/*В регистр совпадения OCR0A записано число 200.
Так как включена функция сброса при совпадении в канале А,
то при совпадении с регистром OCR0A, счетный регистр ТСNT0
сбросится в ноль. Одновременно произойдет и прерывание по 
совпадению*/


		ldi		temp,100
		out		OCR0B,temp
/*В регистр совпадения OCR0B записано число 100.
Так как включена функция прерывания при совпадении в канале В,
то при совпадении с счетным регистром ТСNT0 произойдет 
прерывание по совпадению*/


		ldi		temp,(0<<CS02|0<<CS01|1<<CS00)	
		out		TCCR0B,temp		;В регистре TCCR0B, устанавливаем желаемый коэффициент деления


		sei		;ГЛОБАЛЬНОЕ РАЗРЕШЕНИЕ ПРЕРЫВАНИЙ


;ОСНОВНОЙ ЦИКЛ
MAIN:	ldi		temp,150
	;	out		OCR0B,temp		;Состояние регистров совпадения можно менять не останавливая таймер
		ldi		temp,250
	;	out		OCR0A,temp		;Состояние регистров совпадения можно менять не останавливая таймер
		rjmp	MAIN



;Cовпадение с OCR0A и обнуление таймера происходит один раз за 201 счетов таймера =>
;частота вызова этого прерывания = (частота таймера)/201
T0_capt_A:		sbi		PIND,1
			;	clr		temp
			;	out		TCCR0B,temp		;Таймер остановлен
				reti		


;Cовпадение с OCR0B так же происходит один раз за 201 счетов таймера =>
;частота вызова этого прерывания = (частота таймера)/201
T0_capt_B:		sbi		PIND,0
				ldi		temp,150
			;	out		OCR0B,temp		;Состояние регистров совпадения можно менять не останавливая таймер
				ldi		temp,250
			;	out		OCR0A,temp		;Состояние регистров совпадения можно менять не останавливая таймер
			;	clr		temp
			;	out		TCNT0,temp		;Принудительно обнулили таймер
				reti

;Переполнение счетного регистра TCNT0
T0_Ovr:			sbi		PIND,2
				reti
